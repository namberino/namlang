class Tree
{
    init(item, depth)
    {
        this.item = item;
        this.depth = depth;

        if (depth > 0)
        {
            var item2 = item + item;
            depth = depth - 1;
            this.left = Tree(item2 - 1, depth);
            this.right = Tree(item2, depth);
        }
        else
        {
            this.left = nil;
            this.right = nil;
        }
    }

    check()
    {
        if (this.left == nil)
        {
            return this.item;
        }

        return this.item + this.left.check() - this.right.check();
    }
}

var min_depth = 4;
var max_depth = 12;
var stretch_depth = max_depth + 1;

var start = clock();

print("Stretch tree depth: " + stretch_depth);
print("Check: " + Tree(0, stretch_depth).check());

var long_lived_tree = Tree(0, max_depth);

// iterations = 2 ** max_depth
var iterations = 1;
var d = 0;

while (d < max_depth)
{
    iterations = iterations * 2;
    d = d + 1;
}

var depth = min_depth;

while (depth < stretch_depth)
{
    var check = 0;
    var i = 1;

    while (i <= iterations)
    {
        check = check + Tree(i, depth).check() + Tree(-i, depth).check();
        i = i + 1;
    }

    print("Num trees: " + iterations * 2);
    print("Depth: " + depth);
    print("Check: " + check);

    iterations = iterations / 4;
    depth = depth + 2;
}

print("");
print("Long lived tree of depth: " + max_depth);
print("Check: " + long_lived_tree.check());
print("Elapsed: " + (clock() - start));
